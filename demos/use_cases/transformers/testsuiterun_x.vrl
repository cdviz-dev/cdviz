output = []
t0 = parse_timestamp!(format_timestamp!(now(), format: "%FT00:00:00+00:00"), format: "%+")
t0 = to_unix_timestamp(t0) - to_int(parse_duration("6d", unit: "s") ?? 6*24*3600)
t0 = t0 + to_int(parse_duration(.body.dts_0, unit: "s") ?? 0.0)

for_each(["queued", "started", "finished"]) -> |_index, value| {
  delta_duration = -1.0
  if value == "queued" && !is_nullish(.body.queued) {
    delta_duration = parse_duration(.body.queued, unit: "s") ?? -1.0
  } else if value == "started" && !is_nullish(.body.started) {
    delta_duration = parse_duration(.body.started, unit: "s") ?? -1.0
  } else if value == "finished" && !is_nullish(.body.finished) {
    delta_duration = parse_duration(.body.finished, unit: "s") ?? -1.0
  }

  if delta_duration >= 0.0 {
    delta_duration = delta_duration + (random_float(0.0, delta_duration * 0.2) ?? 0.0) # +/- 20%
    ts = from_unix_timestamp(t0 + to_int(delta_duration)) ?? now()
    ts = format_timestamp!(ts, format: "%+")
    event_type = "dev.cdevents.testsuiterun." + to_string!(value) + ".0.2.0"

    event = {
      "metadata": .metadata,
      "headers": .headers,
      "body": {
        "context": {
          "version": "0.4.1",
          "id": "0",
          "source": "/demo/use_cases",
          "type": event_type,
          "timestamp": ts,
        },
        "subject": {
          "id": .body.id,
          "type": "testSuiteRun",
          "content": {
            "environment": {
              "id": .body.environment
            }
          }
        }
      }
    }

    # Add testSuite information if present
    if !is_nullish(.body.testSuite_id) {
      event.body.subject.content.testSuite = {
        "id": .body.testSuite_id
      }
      if !is_nullish(.body.testSuite_name) {
        event.body.subject.content.testSuite.name = .body.testSuite_name
      }
    }

    # Add outcome for finished events
    if value == "finished" && !is_nullish(.body.outcome) {
      event.body.subject.content.outcome = .body.outcome
    }

    # Add severity if present (for finished events)
    if value == "finished" && !is_nullish(.body.severity) {
      event.body.subject.content.severity = .body.severity
    }

    # Add reason if present (for finished events)
    if value == "finished" && !is_nullish(.body.reason) {
      event.body.subject.content.reason = .body.reason
    }

    output = push(output, event)
  }
}
output
